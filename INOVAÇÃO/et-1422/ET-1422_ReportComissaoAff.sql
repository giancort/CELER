IF OBJECT_ID('AFFILIATOR_COMMISSION') IS NOT NULL 
	DROP TABLE AFFILIATOR_COMMISSION;
GO
CREATE TABLE AFFILIATOR_COMMISSION (
	[COD_AFF_COMM] INT IDENTITY,
	[COD_USER_CREATE]	INT NOT NULL REFERENCES USERS ([COD_USER]),
	[COD_USER_PAY]		INT NULL REFERENCES USERS ([COD_USER]),
	[COD_AFF]			INT NOT NULL REFERENCES AFFILIATOR ([COD_AFFILIATOR]),
	[ACTIVE]			BIT DEFAULT (1),
	[CREATED_AT]		DATETIME DEFAULT dbo.FN_FUS_UTF(CURRENT_TIMESTAMP),
	[MODIFY_DATE]		DATETIME NULL,
	[INITIAL_DATE]		DATE NULL,
	[FINAL_DATE]		DATE NULL,
	[QUANTITY]			INT DEFAULT(0),
	[TPV]				DECIMAL (22, 6) NULL,
	[AMOUNT]			DECIMAL (22, 6) NULL,
	[PAY_DATE]			DATETIME NULL

	CONSTRAINT PK_AFFILIATOR_COMMISSION PRIMARY KEY (COD_AFF_COMM)
)
GO

IF OBJECT_ID('SP_REPORT_AFFILIATOR_COMMISSION') IS NOT NULL BEGIN
    DROP PROCEDURE SP_REPORT_AFFILIATOR_COMMISSION;
END
GO
CREATE PROCEDURE SP_REPORT_AFFILIATOR_COMMISSION  
(  
	@INITIAL_DATE DATETIME = NULL,              
	@FINAL_DATE DATETIME = NULL,
	@COD_AFF CODE_TYPE READONLY
) AS BEGIN  
  
    DECLARE @QUERY_BASIS NVARCHAR(MAX) = '';      
	SET NOCOUNT ON;      
	SET ARITHABORT ON;      

	SET @QUERY_BASIS = ' 
	SELECT 
		COMM.[COD_AFF_COMM]
		, COMM.[COD_AFF]		           
		, COMM.[ACTIVE]		           
		, COMM.[CREATED_AT]	           
		, COMM.[MODIFY_DATE]	           
		, COMM.[INITIAL_DATE]	           
		, COMM.[FINAL_DATE]	           
		, COMM.[QUANTITY]		            
		, COMM.[TPV]
		, COMM.[AMOUNT]		             
		, COMM.[PAY_DATE]		           
		, IIF(COMM.PAY_DATE IS NOT NULL, ''Pago'', ''Em Aberto'') AS [STATUS]
		, US.[IDENTIFICATION]						 AS [USERNAME_CREATE]
		, USP.[IDENTIFICATION]						 AS [USERNAME_PAY]
		, A.[NAME]									 AS [AFFILIATOR]
		, BIL.[CODE]								 AS [TYPE_BILLING]
		, A.[PLATFORM_NAME]							 AS [PLATFORM]
		, A.[CPF_CNPJ]								 AS [CNPJ]
		, IIF(COND.EXEMPTED IS NOT NULL, IIF(COND.EXEMPTED = 1, ''Isento'', ''Cobrança''), '''') AS [BILLING]

    FROM AFFILIATOR_COMMISSION COMM
             INNER JOIN AFFILIATOR A on COMM.COD_AFF = A.COD_AFFILIATOR 
             LEFT JOIN USERS US ON US.COD_USER = COMM.COD_USER_CREATE 
			 LEFT JOIN USERS USP ON USP.COD_USER = COMM.COD_USER_PAY 
			 LEFT JOIN AFFILIATOR_CONDITIONS COND ON COND.COD_AFF = A.COD_AFFILIATOR AND COND.ACTIVE = 1 
			 LEFT JOIN TYPE_BILLING BIL ON BIL.COD_TYPE_BILLING = COND.COD_TP_BILLING
    WHERE COMM.ACTIVE = 1 '

    IF (SELECT COUNT(*) FROM @COD_AFF) > 0
        SET @QUERY_BASIS = CONCAT(@QUERY_BASIS, ' AND COMM.COD_AFF IN (SELECT CODE FROM @COD_AFF) ');  

	IF ((@INITIAL_DATE IS NOT NULL) AND (@FINAL_DATE IS NOT NULL))      
		SET @QUERY_BASIS = CONCAT(@QUERY_BASIS, ' AND (CAST(COMM.INITIAL_DATE AS DATE) BETWEEN CAST(@INITIAL_DATE AS DATE) AND CAST(@FINAL_DATE AS DATE) ');
		SET @QUERY_BASIS = CONCAT(@QUERY_BASIS, ' OR CAST(COMM.FINAL_DATE AS DATE) BETWEEN CAST(@INITIAL_DATE AS DATE) AND CAST(@FINAL_DATE AS DATE)) ');
			
	EXEC [sp_executesql] @QUERY_BASIS      
     ,N'                                                   
        @INITIAL_DATE DATETIME,                                           
        @FINAL_DATE DATETIME,
		@COD_AFF CODE_TYPE READONLY
        '      
		,@INITIAL_DATE	= @INITIAL_DATE      
		,@FINAL_DATE	= @FINAL_DATE
		,@COD_AFF		= @COD_AFF      
END	
GO	

IF OBJECT_ID('SP_FD_DATA_AFFILIATOR_COMMISSION') IS NOT NULL BEGIN
    DROP PROCEDURE SP_FD_DATA_AFFILIATOR_COMMISSION;
END
GO
CREATE PROCEDURE SP_FD_DATA_AFFILIATOR_COMMISSION  
(  
	@INITIAL_DATE DATETIME = NULL,              
	@FINAL_DATE DATETIME = NULL,
	@COD_AFF CODE_TYPE READONLY
) AS BEGIN  
  
    DECLARE @QUERY_BASIS NVARCHAR(MAX) = '';      
	SET NOCOUNT ON;      
	SET ARITHABORT ON;      
	--SET @FINAL_DATE = DATEADD([MILLISECOND], 999, @FINAL_DATE);  

	SET @QUERY_BASIS = ' 
	SELECT 
		COUNT(X.COD_TRAN) AS [QUANTITY]
		, SUM(X.[AMOUNT]) AS [AMOUNT]
		, SUM(X.[TPV]) AS [TPV]
		, X.[AFFILIATOR]
		, X.[CNPJ]
		, X.[TYPE_BILLING]
		, X.[PLATFORM]
		, X.[COD_AFFILIATOR]
		, X.[BILLING]
	FROM (
		SELECT RCT.COD_TRAN
			, SUM(RCT.LIQUID_VALUE_AFFILIATOR)	AS [AMOUNT]
			, SUM(RCT.AMOUNT)					AS [TPV]
			, A.[NAME]							AS [AFFILIATOR]
			, A.[CPF_CNPJ]						AS [CNPJ]
			, BIL.[CODE]						AS [TYPE_BILLING]
			, A.[PLATFORM_NAME]					AS [PLATFORM]
			, A.[COD_AFFILIATOR]				AS [COD_AFFILIATOR] 
			, IIF(COND.EXEMPTED IS NOT NULL, IIF(COND.EXEMPTED = 1, ''Isento'', ''Ativo''), '''') AS [BILLING]

		FROM [REPORT_CONSOLIDATED_TRANS_SUB] RCT
			JOIN AFFILIATOR A on RCT.COD_AFFILIATOR = A.COD_AFFILIATOR 
			LEFT JOIN AFFILIATOR_CONDITIONS COND ON COND.COD_AFF = A.COD_AFFILIATOR AND COND.ACTIVE = 1 
			LEFT JOIN TYPE_BILLING BIL ON BIL.COD_TYPE_BILLING = COND.COD_TP_BILLING
			LEFT JOIN AFFILIATOR_COMMISSION COMM ON COMM.COD_AFF = RCT.COD_AFFILIATOR
				AND (CAST(COMM.INITIAL_DATE AS DATE) BETWEEN CAST(@INITIAL_DATE AS DATE) AND CAST(@FINAL_DATE AS DATE)
				OR CAST(COMM.FINAL_DATE AS DATE) BETWEEN CAST(@INITIAL_DATE AS DATE) AND CAST(@FINAL_DATE AS DATE)) 

		WHERE RCT.COD_SITUATION IN (3) 
			AND COMM.COD_AFF_COMM IS NULL '

    IF (SELECT COUNT(*) FROM @COD_AFF) > 0
        SET @QUERY_BASIS = CONCAT(@QUERY_BASIS, ' AND RCT.COD_AFFILIATOR IN (SELECT CODE FROM @COD_AFF) ');  

	IF ((@INITIAL_DATE IS NOT NULL) AND (@FINAL_DATE IS NOT NULL))      
		SET @QUERY_BASIS = CONCAT(@QUERY_BASIS, ' AND CAST(RCT.TRANSACTION_DATE AS DATE) BETWEEN CAST(@INITIAL_DATE AS DATE) AND CAST(@FINAL_DATE AS DATE) ');

	SET @QUERY_BASIS = CONCAT(@QUERY_BASIS, ' GROUP BY RCT.COD_TRAN, A.[COD_AFFILIATOR], A.[NAME], A.[CPF_CNPJ], A.[PLATFORM_NAME], BIL.[CODE], COND.EXEMPTED 
		) AS X
		GROUP BY X.[COD_AFFILIATOR], X.[AFFILIATOR], X.[CNPJ], X.[PLATFORM], X.[TYPE_BILLING], X.[BILLING] ');
			
	EXEC [sp_executesql] @QUERY_BASIS      
     ,N'                                                   
        @INITIAL_DATE DATETIME,                                           
        @FINAL_DATE DATETIME,
		@COD_AFF CODE_TYPE READONLY
        '      
		,@INITIAL_DATE	= @INITIAL_DATE      
		,@FINAL_DATE	= @FINAL_DATE
		,@COD_AFF		= @COD_AFF      
END	
GO	

IF TYPE_ID('TP_AFF_COMISSION') IS NOT NULL BEGIN
	IF OBJECT_ID('SP_REG_AFFILIATOR_COMMISSION') IS NOT NULL DROP PROCEDURE SP_REG_AFFILIATOR_COMMISSION
	IF OBJECT_ID('SP_UP_AFFILIATOR_COMMISSION') IS NOT NULL DROP PROCEDURE SP_UP_AFFILIATOR_COMMISSION
    DROP TYPE [TP_AFF_COMISSION]
END
GO
CREATE TYPE [TP_AFF_COMISSION] AS TABLE(
	COD_AFF_COMM	INT NULL
	, COD_AFF		INT NULL
	, QUANTITY		INT	NULL
	, TPV			DECIMAL(22,6) NULL
	, AMOUNT		DECIMAL(22,6) NULL
	, PAYMENT		DECIMAL(22,6) NULL
)
GO

IF OBJECT_ID('SP_REG_AFFILIATOR_COMMISSION') IS NOT NULL BEGIN
    DROP PROCEDURE SP_REG_AFFILIATOR_COMMISSION;
END
GO
CREATE PROCEDURE SP_REG_AFFILIATOR_COMMISSION  
(  
    @ITEMS TP_AFF_COMISSION READONLY ,  
    @COD_USER INT NULL ,
	@INITIAL_DATE DATETIME = NULL,              
	@FINAL_DATE DATETIME = NULL
) AS 
BEGIN  

	INSERT INTO AFFILIATOR_COMMISSION (COD_AFF, COD_USER_CREATE, INITIAL_DATE, FINAL_DATE, QUANTITY, TPV, AMOUNT)  
	SELECT  
	I.COD_AFF  
    , @COD_USER
	, dbo.FN_FUS_UTF(@INITIAL_DATE)
    , dbo.FN_FUS_UTF(@FINAL_DATE)
    , I.QUANTITY
	, I.TPV
	, I.AMOUNT
	
	FROM @ITEMS I
	LEFT JOIN AFFILIATOR_COMMISSION COMM ON COMM.COD_AFF = I.COD_AFF
		AND (CAST(COMM.INITIAL_DATE AS DATE) BETWEEN CAST(@INITIAL_DATE AS DATE) AND CAST(@FINAL_DATE AS DATE)
		OR CAST(COMM.FINAL_DATE AS DATE) BETWEEN CAST(@INITIAL_DATE AS DATE) AND CAST(@FINAL_DATE AS DATE))
	WHERE COMM.COD_AFF_COMM IS NULL
END
GO

IF OBJECT_ID('SP_UP_AFFILIATOR_COMMISSION') IS NOT NULL BEGIN
    DROP PROCEDURE SP_UP_AFFILIATOR_COMMISSION;
END
GO
CREATE PROCEDURE SP_UP_AFFILIATOR_COMMISSION  
(  
    @ITEMS TP_AFF_COMISSION READONLY,  
    @COD_USER INT NULL
) AS 
BEGIN  

	--UPDATE AFFILIATOR_COMMISSION SET ACTIVE = 0
	--FROM AFFILIATOR_COMMISSION COMM
	--JOIN @ITEMS I ON I.COD_AFF = COMM.COD_AFF AND I.COD_AFF_COMM = COMM.COD_AFF_COMM
	--WHERE COMM.ACTIVE = 1 AND COMM.PAY_DATE IS NOT NULL

	--INSERT INTO AFFILIATOR_COMMISSION (COD_AFF, COD_USER_CREATE, INITIAL_DATE, FINAL_DATE, QUANTITY, TPV, AMOUNT, PAY_DATE, COD_USER_PAY)  
	--SELECT  
	--I.COD_AFF  
    --, COMM.COD_USER_CREATE
	--, COMM.INITIAL_DATE
    --, COMM.FINAL_DATE
    --, COMM.QUANTITY
	--, COMM.TPV
	--, COMM.AMOUNT
	--, dbo.FN_FUS_UTF(CURRENT_TIMESTAMP)
	--, @COD_USER
	--FROM AFFILIATOR_COMMISSION COMM 
	--JOIN @ITEMS I ON I.COD_AFF = COMM.COD_AFF AND I.COD_AFF_COMM = COMM.COD_AFF_COMM
	--WHERE COMM.ACTIVE = 0 AND COMM.PAY_DATE IS NOT NULL

	UPDATE AFFILIATOR_COMMISSION SET COD_USER_PAY = @COD_USER
	, PAY_DATE = dbo.FN_FUS_UTF(CURRENT_TIMESTAMP)
	FROM AFFILIATOR_COMMISSION COMM
	JOIN @ITEMS I ON I.COD_AFF = COMM.COD_AFF AND I.COD_AFF_COMM = COMM.COD_AFF_COMM
	WHERE COMM.ACTIVE = 1 AND COMM.PAY_DATE IS NULL

END
GO